buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = '1.4.2-1.12.2'
group = 'mchorse.skin_n_bones'
archivesBaseName = 'skin_n_bones'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            jvmArgs '-Xmx1G'
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            jvmArgs '-Xmx1G'
        }
    }
}

repositories {
    maven {
        url = "https://cursemaven.com"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    // External mod dependencies (dev jars, not recommended in prod)
    implementation fg.deobf("curse.maven:mchorses-mclib-304750:5440933")
    implementation fg.deobf("curse.maven:metamorph-256932:5440927")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Skin n Bones",
                "Specification-Vendor"    : "mchorse",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "mchorse",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy('reobfJar')
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", "1.12.2"

    filesMatching('mcmod.info') {
        expand 'version': project.version, 'mcversion': "1.12.2"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
